plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'maven-publish'
	id 'jacoco'
}

group = 'com.camel'
version = '0.0.1-SNAPSHOT'

jacoco {
	toolVersion = "0.8.11"
}

test {
	finalizedBy jacocoTestReport
	useJUnitPlatform()
}


java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	camelVersion = '4.6.0'
	springCloudVersion = "2023.0.3"
	springBootVersion = "3.3.4"
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-web:3.3.4"
	implementation "org.apache.camel.springboot:camel-spring-boot-starter:${camelVersion}"
	implementation "org.apache.camel.springboot:camel-kafka-starter:${camelVersion}"
	implementation "org.apache.camel.springboot:camel-bindy-starter:${camelVersion}"
	implementation "org.apache.camel.springboot:camel-jackson-starter:${camelVersion}"
	implementation "org.apache.camel:camel-core:${camelVersion}"
	implementation "org.apache.camel:camel-resilience4j:${camelVersion}"
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation "org.apache.camel:camel-csv:${camelVersion}"
	implementation "net.logstash.logback:logstash-logback-encoder:7.2"
	implementation "org.apache.camel:camel-jms:${camelVersion}"
	implementation "org.apache.camel:camel-activemq:${camelVersion}"
	implementation "javax.jms:javax.jms-api:2.0.1"
	implementation "jakarta.jms:jakarta.jms-api:3.1.0"
	implementation "com.ibm.mq:com.ibm.mq.allclient:9.4.0.0"
	implementation "com.ibm.mq:com.ibm.mq.jakarta.client:9.4.0.0"
	implementation "com.ibm.mq:mq-jms-spring-boot-starter:3.2.4"
	compileOnly "org.projectlombok:lombok:1.18.30"
	annotationProcessor "org.projectlombok:lombok:1.18.30"
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.kafka:spring-kafka-test"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

spotless {
	format 'misc', {
		target '*.gradle', '.gitignore'
		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	format 'README.md', {
		target '*.md'
		endWithNewline()
	}
	java {
		toggleOffOn()
		palantirJavaFormat()
		removeUnusedImports()
		trimTrailingWhitespace()
		endWithNewline()
	}
}

tasks.named('build') {
	dependsOn 'spotlessCheck'
}

configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.group == 'org.bitbucket.b_c' && details.requested.name == 'jose4j') {
			details.useVersion('0.9.6')
		} else if (details.requested.group == 'org.springframework' && details.requested.name == 'spring-webmvc') {
			details.useVersion('6.1.13')
		}

	}
}
tasks.named('test') {
	useJUnitPlatform()
}
